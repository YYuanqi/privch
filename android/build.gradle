buildscript {
    def versionMajor = 0 //12bit
    def versionMinor = 1 //10bit
    def versionRevision = 1 //10bit

    ext {
        buildToolsVersion = "31.0.0"
        ndkVersion = "22.1.7171670"
        compileSdkVersion = 31
        targetSdkVersion = 31
        minSdkVersion = 26

        buildHost = "${InetAddress.localHost.hostName}"
        buildUser = "${System.getProperty('user.name')}"
        buildTime = new Date().format("yyyy-MM-dd")

        versionCode = (versionMajor << 20) | (versionMinor << 10) | versionRevision
        versionName = "$versionMajor.$versionMinor.$versionRevision"

        /*
            https://developer.android.com/studio/build/shrink-code
            resConfigs property remove alternative resource files that your app does not need
         */
        resConfigs = ["en", "zh-rCN"]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

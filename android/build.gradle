buildscript {
    def versionMajor = 0 //12bit
    def versionMinor = 1 //10bit
    def versionRevision = 0 //10bit

    ext {
        kotlinVersion = "1.5.0"
        buildToolsVersion = "30.0.3"
        ndkVersion = "22.1.7171670"
        compileSdkVersion = 30
        minSdkVersion = 26
        targetSdkVersion = 30

        buildHost = "${InetAddress.localHost.hostName}"
        buildUser = "${System.getProperty('user.name')}"
        buildTime = new Date().format("yyyy-MM-dd")

        versionCode = (versionMajor << 20) | (versionMinor << 10) | versionRevision
        versionName = "$versionMajor.$versionMinor.$versionRevision"

        /*
            https://developer.android.com/studio/build/shrink-code
            resConfigs property remove alternative resource files that your app does not need
         */
        resConfigs = ["en", "zh-rCN"]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new Exception("flutter.sdk not found in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionCode == null || flutterVersionName == null) {
    throw new GradleException("flutter.versionCode or flutter.versionName not found in local.properties file.")
}

def isXinlake = System.getProperty("user.name").toLowerCase().contentEquals("xinlake")
if (isXinlake) {
    apply from: "xinlake.gradle"
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    buildToolsVersion "32.0.0"
    ndkVersion "23.1.7779620"
    compileSdkVersion 32

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "xinlake.privch"
        targetSdkVersion 32
        minSdkVersion 26

        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        resValue "string", "gradle_build_host", "${InetAddress.localHost.hostName}"
        resValue "string", "gradle_build_user", "${System.getProperty('user.name')}"
        resValue "string", "gradle_build_time", new Date().format("yyyy-MM-dd")
        // buildConfigField "String", "buildHost", buildHost
        // buildConfigField "String", "buildUser", buildUser
        // buildConfigField "String", "buildTime", buildTime

        setProperty("archivesBaseName", applicationId + "-v" + versionName)

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    signingConfigs {
        if (isXinlake) {
            xinlake {
                storeFile file(signingStoreFile)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
    }

    buildTypes {
        debug {
            signingConfig isXinlake ? signingConfigs.xinlake : signingConfigs.debug
        }
        profile {
            signingConfig isXinlake ? signingConfigs.xinlake : signingConfigs.debug
        }
        release {
            signingConfig isXinlake ? signingConfigs.xinlake : signingConfigs.debug
        }
    }

    // custom apk name and copy apk
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFileName?.endsWith('.apk')) {
                // copy apk
                variant.assembleProvider.get().doLast {
                    copy {
                        from output.outputFile
                        into "../../../.app"
                    }
                }
            }
        }
    }
}

flutter {
    source '../..'
}
